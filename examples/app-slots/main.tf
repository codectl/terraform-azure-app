module "naming" {
  source  = "codectl/naming/azure"
  version = "~> 0.1"

  suffix = ["demo", "dev"]
}

module "regions" {
  source  = "codectl/locations/azure"
  version = "~> 1.0"

  location = {
    primary = "westeurope"
  }
}

module "rg" {
  source  = "codectl/rg/azure"
  version = "~> 1.0"

  groups = {
    demo = {
      name     = module.naming.resource_group.name_unique
      location = module.regions.location.primary.name
    }
  }
}

module "appservice" {
  source  = "codectl/plan/azure"
  version = "~> 1.0"

  resource_group_name = module.rg.groups.demo.name
  location            = module.rg.groups.demo.location

  plans = {
    web = {
      name     = module.naming.app_service_plan.name
      os_type  = "Linux"
      sku_name = "P1v3"
    }
  }
}

module "storage1" {
  source  = "codectl/sa/azure"
  version = "~> 1.0"

  storage = {
    name                = "${module.naming.storage_account.name_unique}1"
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
  }
}

module "storage2" {
  source  = "codectl/sa/azure"
  version = "~> 1.0"

  storage = {
    name                = "${module.naming.storage_account.name_unique}2"
    location            = module.rg.groups.demo.location
    resource_group_name = module.rg.groups.demo.name
  }
}

module "webapp" {
  source  = "codectl/app/azure"
  version = "~> 1.0"

  resource_group_name = module.rg.groups.demo.name
  location            = module.rg.groups.demo.location

  web_app = {
    type            = "linux"
    name            = module.naming.app_service.name_unique
    service_plan_id = module.appservice.plans.web.id
    slots           = local.slots

    connection_strings = {
      storage1 = {
        name  = "storage_conn"
        type  = "Custom"
        value = module.storage1.account.primary_connection_string
      }
      storage2 = {
        name  = "storage1"
        type  = "Custom"
        value = module.storage2.account.primary_connection_string
      }
    }

    site_config = {
      auto_heal_setting = {
        action = {
          action_type = "Recycle"
        }
        trigger = {
          requests = {
            count    = 3
            interval = "00:01:00"
          }

          status_code = {
            server_errors = {
              count             = 10
              interval          = "00:01:00"
              status_code_range = "500-599"
            }
            not_found = {
              count             = 25
              interval          = "00:05:00"
              status_code_range = "404"
              path              = "/api/health"
            }
          }

          slow_request_with_path = {
            api = {
              count      = 5
              interval   = "00:01:00"
              time_taken = "00:00:30"
              path       = "/api"
            }
            reports = {
              count      = 3
              interval   = "00:05:00"
              time_taken = "00:01:00"
              path       = "/reports"
            }
          }
        }
      }
    }
  }
}
